Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION FLAG_COMMANDS
IF
GlobalFlagSet("LLPARTY_Commands_IncreaseMaxPartySize")
AND
NOT DB_LLPARTY_Temp_NextMaxPartySize(_)
AND
DB_LLPARTY_MaxPartySize(_Max)
THEN
DB_LLPARTY_Temp_NextMaxPartySize(_Max);

IF
GlobalFlagSet("LLPARTY_Commands_DecreaseMaxPartySize")
AND
NOT DB_LLPARTY_Temp_NextMaxPartySize(_)
AND
DB_LLPARTY_MaxPartySize(_Max)
THEN
DB_LLPARTY_Temp_NextMaxPartySize(_Max);

QRY
LLPARTY_QRY_MaxBelowLimit((INTEGER)_Max)
AND
_Max <= 10
THEN
DB_NOOP(1);

QRY
LLPARTY_QRY_MaxBelowLimit((INTEGER)_Max)
AND
_Max > 10
AND
DB_GlobalFlag("LLPARTY_Debug_UnlimitedPartySize")
THEN
DB_NOOP(1);

IF
GlobalFlagSet("LLPARTY_Commands_IncreaseMaxPartySize")
AND
LLPARTY_QRY_ClearGlobalFlag("LLPARTY_Commands_IncreaseMaxPartySize")
AND
DB_LLPARTY_Temp_NextMaxPartySize(_Max)
AND
IntegerSum(_Max, 1, _NextMax)
AND
LLPARTY_QRY_MaxBelowLimit(_NextMax)
AND
IntegertoString(_NextMax, _NextStr)
THEN
LLPARTY_Log("[LLPARTY:Settings:IncreaseMaxPartySize] Next max party size set to [",_NextStr,"].");
SysClear("DB_LLPARTY_Temp_NextMaxPartySize", 1);
DB_LLPARTY_Temp_NextMaxPartySize(_NextMax);
LLPARTY_Settings_UpdateDialogVars();
GlobalSetFlag("LLPARTY_Settings_MaxPartySizeChanged");

IF
GlobalFlagSet("LLPARTY_Commands_DecreaseMaxPartySize")
AND
LLPARTY_QRY_ClearGlobalFlag("LLPARTY_Commands_DecreaseMaxPartySize")
AND
DB_LLPARTY_Temp_NextMaxPartySize(_Max)
AND
IntegerSubtract(_Max, 1, _NextMax)
AND
_NextMax >= 4
AND
IntegertoString(_NextMax, _NextStr)
THEN
LLPARTY_Log("[LLPARTY:Settings:IncreaseMaxPartySize] Next max party size set to [",_NextStr,"].");
SysClear("DB_LLPARTY_Temp_NextMaxPartySize", 1);
DB_LLPARTY_Temp_NextMaxPartySize(_NextMax);
LLPARTY_Settings_UpdateDialogVars();
GlobalSetFlag("LLPARTY_Settings_MaxPartySizeChanged");

IF
GlobalFlagSet("LLPARTY_Commands_ConfirmPartySizeChanges")
AND
LLPARTY_QRY_ClearGlobalFlag("LLPARTY_Commands_ConfirmPartySizeChanges")
AND
DB_LLPARTY_MaxPartySize(_Max)
AND
DB_LLPARTY_Temp_NextMaxPartySize(_NextMax)
AND
_Max != _NextMax
AND
IntegertoString(_NextMax, _IntStr)
THEN
LLPARTY_Log("[LLPARTY:Settings:ConfirmPartySizeChanges] Setting max party size to [",_IntStr,"] and re-installing.");
SysClear("DB_LLPARTY_Temp_NextMaxPartySize", 1);
SysClear("DB_LLPARTY_MaxPartySize", 1);
DB_LLPARTY_MaxPartySize(_NextMax);
Proc_CheckPartyFull();
LLPARTY_InstallMod();
LLPARTY_Settings_UpdateDialogVars();

IF
GlobalFlagSet("LLPARTY_Commands_ConfirmPartySizeChanges")
AND
LLPARTY_QRY_ClearGlobalFlag("LLPARTY_Commands_ConfirmPartySizeChanges")
AND
DB_LLPARTY_MaxPartySize(_Max)
AND
DB_LLPARTY_Temp_NextMaxPartySize(_NextMax)
AND
_Max == _NextMax
THEN
SysClear("DB_LLPARTY_Temp_NextMaxPartySize", 1);
LLPARTY_Log("[LLPARTY:Settings:ConfirmPartySizeChanges] Next party size and current match. Skipping re-initialization.");
LLPARTY_Settings_UpdateDialogVars();

IF
GlobalFlagSet("LLPARTY_Commands_ConfirmPartySizeChanges")
AND
DB_LLPARTY_MaxPartySize(6)
AND
DB_DialogName("LLPARTY_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
THEN
ObjectSetFlag(_Player, "LLPARTY_DefaultMaxPartySizeIsSet");

IF
GlobalFlagSet("LLPARTY_Commands_ConfirmPartySizeChanges")
THEN
GlobalClearFlag("LLPARTY_Settings_MaxPartySizeChanged");

IF
GlobalFlagSet("LLPARTY_Commands_CancelPartySizeChanges")
AND
LLPARTY_QRY_ClearGlobalFlag("LLPARTY_Commands_CancelPartySizeChanges")
THEN
SysClear("DB_LLPARTY_Temp_NextMaxPartySize", 1);
LLPARTY_Settings_UpdateDialogVars();
GlobalClearFlag("LLPARTY_Settings_MaxPartySizeChanged");

IF
GlobalFlagSet("LLPARTY_Commands_ReInitPartySize")
THEN
GlobalClearFlag("LLPARTY_Commands_ReInitPartySize");
Proc_CheckPartyFull();
LLPARTY_InstallMod();

IF
GlobalFlagSet("LLPARTY_Commands_ResetPartySizeToDefault")
THEN
LLPARTY_Log("[LLPARTY:Settings:ResetPartySizeToDefault] Next max party size set to [6].");
GlobalClearFlag("LLPARTY_Commands_ResetPartySizeToDefault");
SysClear("DB_LLPARTY_Temp_NextMaxPartySize", 1);
DB_LLPARTY_Temp_NextMaxPartySize(6);
LLPARTY_Settings_UpdateDialogVars();
GlobalSetFlag("LLPARTY_Settings_MaxPartySizeChanged");

IF
GlobalFlagSet("LLPARTY_Commands_ResetPartySizeToDOS2DEDefault")
THEN
LLPARTY_Log("[LLPARTY:Settings:ResetPartySizeToDefault] Next max party size set to [4].");
GlobalClearFlag("LLPARTY_Commands_ResetPartySizeToDOS2DEDefault");
SysClear("DB_LLPARTY_Temp_NextMaxPartySize", 1);
DB_LLPARTY_Temp_NextMaxPartySize(4);
LLPARTY_Settings_UpdateDialogVars();
GlobalSetFlag("LLPARTY_Settings_MaxPartySizeChanged");
//END_REGION

//REGION DIALOG_EVENTS
PROC
LLPARTY_Settings_UpdateDialogVars()
AND
DB_LLPARTY_Temp_NextMaxPartySize(_NextMax)
THEN
DialogSetVariableInt("LLPARTY_SettingsMenu", "LLPARTY_MaxPartySize_6e79a6d2-a814-4efa-8ccd-1b9d4810024b", _NextMax);

PROC
LLPARTY_Settings_UpdateDialogVars()
AND
NOT DB_LLPARTY_Temp_NextMaxPartySize(_)
AND
DB_LLPARTY_MaxPartySize(_Max)
THEN
DialogSetVariableInt("LLPARTY_SettingsMenu", "LLPARTY_MaxPartySize_6e79a6d2-a814-4efa-8ccd-1b9d4810024b", _Max);

IF
DialogStarted("LLPARTY_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
CharacterGetHostCharacter(_Host)
AND
CharacterGetReservedUserID(_Player, _UserID)
AND
CharacterGetReservedUserID(_Host, _HostUserID)
AND
GetUserProfileID(_HostUserID, _ProfileID)
AND
GetUserProfileID(_UserID, _ProfileID)
THEN
ObjectSetFlag(_Player, "LLPARTY_IsHost", _Instance);
LLPARTY_Settings_UpdateDialogVars();

IF
DialogStarted("LLPARTY_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
AND
DB_LLPARTY_MaxPartySize(6)
THEN
ObjectSetFlag(_Player, "LLPARTY_DefaultMaxPartySizeIsSet");

IF
DialogEnded("LLPARTY_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
ObjectSetFlag(_Player, "LLPARTY_IsHost", _Instance);

IF
DialogEnded("LLPARTY_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
THEN
ObjectClearFlag(_Player, "LLPARTY_DefaultMaxPartySizeIsSet");
//END_REGION

//REGION OPEN_EVENTS
IF
TextEventSet("llparty_settings")
AND
CharacterGetHostCharacter(_Host)
THEN
Proc_StartDialog(0, "LLPARTY_SettingsMenu", _Host, _Host);

PROC
ProcBlockUseOfItem(_Player, _Book)
AND
GetTemplate(_Book, "BOOK_LLPARTY_SettingsMenu_a72cac8f-499b-4822-81ab-62b78b487ea0")
AND
CharacterIsInCombat(_Player, 1)
THEN
CharacterStatusText(_Player, "<font color='#FF0000'>*You can't use that in combat!*</font>");
DB_CustomUseItemResponse(_Player, _Book, 0);

IF
CharacterUsedItemTemplate(_Player, "BOOK_LLPARTY_SettingsMenu_a72cac8f-499b-4822-81ab-62b78b487ea0", _Book)
AND
QRY_SpeakerIsAvailable(_Player)
THEN
Proc_StartDialog(0, "LLPARTY_SettingsMenu", _Player, _Player);
//END_REGION

//REGION ADD_EVENTS
//Multiplayer lobby error
PROC
LLPARTY_Updater_VersionChanged((STRING)_OldVersion, "1.1.1.0")
THEN
GlobalClearFlag("LLPARTY_AddedInitialBook");

IF
GameStarted(_Level,_)
AND
IsGameLevel(_Level, 1)
AND
NOT GlobalGetFlag("LLPARTY_AddedInitialBook", 1)
THEN
TimerCancel("LLPARTY_Timers_MenuBookCheck");
TimerLaunch("LLPARTY_Timers_MenuBookCheck", 500);

IF
TimerFinished("LLPARTY_Timers_MenuBookCheck")
AND
CharacterGetHostCharacter(_Host)
AND
ItemTemplateIsInPartyInventory(_Host, "BOOK_LLPARTY_SettingsMenu_a72cac8f-499b-4822-81ab-62b78b487ea0", 0, 0)
THEN
ItemTemplateAddTo("BOOK_LLPARTY_SettingsMenu_a72cac8f-499b-4822-81ab-62b78b487ea0", _Host, 1, 1);

IF
TimerFinished("LLPARTY_Timers_MenuBookCheck")
AND
CharacterGetHostCharacter(_Host)
AND
ItemTemplateIsInPartyInventory(_Host, "BOOK_LLPARTY_SettingsMenu_a72cac8f-499b-4822-81ab-62b78b487ea0", 0, _Count)
AND
_Count > 0
THEN
GlobalSetFlag("LLPARTY_AddedInitialBook");
//END_REGION

//REGION COMMANDS
IF
ObjectFlagSet("LLPARTY_Commands_AddCharacterSwitcherSkills", (CHARACTERGUID)_Player, _Instance)
THEN
ObjectClearFlag(_Player, "LLPARTY_Commands_AddCharacterSwitcherSkills", _Instance);
LLPARTY_CharacterSwitcher_AddSkills(_Player);

IF
ObjectFlagSet("LLPARTY_Commands_RemoveCharacterSwitcherSkills", (CHARACTERGUID)_Player, _Instance)
THEN
ObjectClearFlag(_Player, "LLPARTY_Commands_RemoveCharacterSwitcherSkills", _Instance);
LLPARTY_CharacterSwitcher_RemoveSkills(_Player);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_PartySizeEvolved"
